
autoload -Uz vcs_info

zstyle ':vcs_info:*' disable cvs svn bzr cdv darcs mtn tla hg p4 svk
zstyle ':vcs_info:*' enable  git

zstyle ':vcs_info:*' check-for-changes  false
zstyle ':vcs_info:*' formats        "[%b]"
zstyle ':vcs_info:*' actionformats  "[%b|%a]"
zstyle ':vcs_info:*' stagedstr      "!"
zstyle ':vcs_info:*' unstagedstr    "?"

function title {
  if [[ $TERM == "screen" ]]; then
    # Use these two for GNU Screen:
    print -nR $'\033k'$1$'\033'\\

    print -nR $'\033]0;'$2$'\a'
  elif [[ $TERM == "xterm" || $TERM == "rxvt" ]]; then
    # Use this one instead for XTerms:
    print -nR $'\033]0;'$*$'\a'
  fi
}

function precmd() {
  title zsh "$PWD"

  psvar=()

  vcs_info
  [[ -n $vcs_info_msg_0_ ]] && psvar[1]="$vcs_info_msg_0_"
}

function preexec {
  emulate -L zsh
  local -a cmd; cmd=(${(z)1})
  title $cmd[1]:t "$cmd[2,-1]"
}

setopt prompt_subst

PS1="%{%B%F{green}%}%(!..%n@)%m%b%F{green}(%y)%b%F{red}%1v${PSEXTRA}%f%b %{%B%F{yellow}%}%~ %f%B%#%b "
RPROMPT="%B%(0?..%F{red}[%?]%f):%!"




# vim:ft=zsh
